apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "com.diffplug.spotless"
apply plugin: "maven-publish"
apply plugin: "jacoco"

apply from: '../jacoco/modules.gradle'

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    defaultConfig {

        minSdkVersion 27
        targetSdkVersion 32
        versionCode project.property("version_code").toInteger()
        versionName project.property("version_name") as String

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            testCoverageEnabled true
            debuggable true
        }
        debug {
            testCoverageEnabled true
        }
    }

    buildFeatures {
        viewBinding true
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:1.8.0"
    implementation "androidx.appcompat:appcompat:1.4.2"
    api "com.github.kuamanet:android-native-opencv:0.1"
    // ViewModel
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    // LiveData
    api "androidx.lifecycle:lifecycle-livedata-ktx:2.5.1"
    // Lifecycles only (without ViewModel or LiveData)
    api "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"

    implementation "androidx.activity:activity-ktx:1.5.1"
    implementation "androidx.fragment:fragment-ktx:1.5.1"

    // CameraX core library using camera2 implementation
    implementation "androidx.camera:camera-camera2:1.2.0-alpha03"
    // CameraX Lifecycle Library
    implementation "androidx.camera:camera-lifecycle:1.2.0-alpha03"
    // CameraX View class
    implementation "androidx.camera:camera-view:1.2.0-alpha03"

    // zoomable image view
    implementation "com.github.chrisbanes:PhotoView:2.3.0"
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "androidx.exifinterface:exifinterface:1.3.3"

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "com.github.kuamanet:android-native-opencv:0.1"

    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0'

    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"
}

spotless {
    kotlin {
        target "**/*.kt"
        // EditorConfig support is broken: https://github.com/diffplug/spotless/issues/142
        ktlint("0.35.0").userData(["disabled_rules": "no-wildcard-imports,import-ordering,chain-wrapping"])
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.kotlin.srcDirs
    from android.sourceSets.main.java.srcDirs
    from fileTree(dir: 'src/libs', include: ['*.jar'])
    classifier "sources"
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // Adds javadocs and sources as separate jars.
                // artifact androidJavadocsJar
                artifact androidSourcesJar

                groupId 'net.kuama.android'
                artifactId 'documentscanner'
                version project.property('version_name')
            }
        }
    }
}
